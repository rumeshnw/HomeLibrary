plugins {
	id 'java'
	id 'groovy'
	id 'org.springframework.boot' version '3.2.6'
	id 'io.spring.dependency-management' version '1.1.4'
    id 'nu.studer.jooq' version '8.2'
	id 'org.liquibase.gradle' version '2.2.2'
}

group = 'dev.rumesh'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '21'
	targetCompatibility = '21'
}

ext {

}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-jooq'
	implementation 'mysql:mysql-connector-java:8.0.33'
	implementation 'org.liquibase:liquibase-core:4.28.0'

	compileOnly 'org.projectlombok:lombok'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.codehaus.groovy:groovy'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
    testImplementation 'org.spockframework:spock-spring:2.3-groovy-4.0'

	liquibaseRuntime 'mysql:mysql-connector-java:8.0.33'
	liquibaseRuntime 'org.liquibase:liquibase-core:4.28.0'
	liquibaseRuntime 'info.picocli:picocli:4.7.6'
	liquibaseRuntime 'ch.qos.logback:logback-core:1.5.6'
	liquibaseRuntime 'ch.qos.logback:logback-classic:1.5.6'
}

tasks.named('test') {
	useJUnitPlatform()
}

def dbUrl = System.getenv('DB_URL')
def dbUsername = System.getenv('DB_USERNAME')
def dbPassword = System.getenv('DB_PASSWORD')

liquibase {
    activities {
        main {
            changelogFile './src/main/resources/db/changelog/db.changelog-master.xml'
            url "${dbUrl}"
            username "${dbUsername}"
            password "${dbPassword}"
            driver 'com.mysql.cj.jdbc.Driver'
        }
    }
    runList = 'main'
}

springBoot {
	mainClass = 'dev.rumesh.homelibrary.HomelibraryApplication'
}
